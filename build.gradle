plugins {
    id 'java'
    id 'org.beryx.jlink' version '2.0.1-gradle4'
}

sourceCompatibility = 11
targetCompatibility = 11
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

repositories {
    mavenCentral()
}

ext {
    launcherName = 'makerplayground'
    launcherVersion = '0.3.0-beta1'
    imageDirPath = "$buildDir/image"
    imageZipPath = "$buildDir/image-zip/${launcherName}-${launcherVersion}.zip"
}

defaultTasks 'clean', 'run'

dependencies {
    compile "org.openjfx:javafx-base:11:${platform}"
    compile "org.openjfx:javafx-graphics:11:${platform}"
    compile "org.openjfx:javafx-controls:11:${platform}"
    compile "org.openjfx:javafx-fxml:11:${platform}"
    compile "com.fazecast:jSerialComm:2.2.2"
    compile "org.controlsfx:controlsfx:9.0.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.7"
    compile "commons-io:commons-io:2.6"
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls', 
                '--add-modules', 'javafx.fxml'
        ]
    }
}

run {
    doFirst {
        jvmArgs += [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls',
                '--add-modules', 'javafx.fxml',
                '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
                '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.inputmap=ALL-UNNAMED',
                '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
                '--add-exports', 'javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED'
        ]
    }
}

jlink {
    launcher {
        name = project.launcherName
        jvmArgs = ["--add-modules javafx.controls",
                "--add-modules javafx.fxml",
                "--add-reads MakerPlayground.merged.module=javafx.base,javafx.graphics,javafx.controls",
                "--add-exports=javafx.base/com.sun.javafx.runtime=MakerPlayground.merged.module",
                "--add-exports=javafx.controls/com.sun.javafx.scene.control.inputmap=MakerPlayground.merged.module",
                "--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=MakerPlayground.merged.module",
                "--add-exports=javafx.graphics/com.sun.javafx.scene.traversal=MakerPlayground.merged.module"]
    }
    mergedModule {
        requires 'java.sql';
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

mainClassName='io.makerplayground.ui.Main'